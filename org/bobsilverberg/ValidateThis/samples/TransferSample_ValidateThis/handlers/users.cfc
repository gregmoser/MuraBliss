<!--------------------------------------------------------------------
Generated Application: TransferSample
Generated Date: March 19 2007
Generated By: ColdBox Ant Task.
---------------------------------------------------------------------->
<!-----------------------------------------------------------------------
Template : ehGeneral.cfc
Author 	 : Luis Majano
Date     : 3/19/2007 3:28:35 PM
Description :
	This is the main handler for the Transfer sample

Modification History:
3/19/2007 - Created Template
---------------------------------------------------------------------->
<cfcomponent name="users" extends="coldbox.system.eventhandler" output="false" autowire="true">
	
	<!--- Dependency Injection from the cache --->
	<cfproperty name="Transfer" type="ocm" scope="instance" />


	<cffunction name="dspHome" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<!--- Set the View To Display --->
		<cfset Event.setView("vwHome")>
	</cffunction>

	<cffunction name="dspUsers" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<cfset var rc = event.getCollection()>
		<!--- Do Your Logic Here to prepare a view --->
		<cfset rc.Users = instance.transfer.list("users.users","lname",false)>
		<!--- Set the View To Display, after Logic --->
		<cfset Event.setView("vwListing")>
	</cffunction>

	<cffunction name="doDelete" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<cfset var rc = event.getCollection()>
		<cfset var i = 1>
		<!--- Do Your Logic Here to prepare a view --->
		<cfset event.paramValue("idlist","")>
		<cfloop list="#rc.idlist#" index="i">
			<cfset instance.transfer.delete(instance.transfer.get("users.users",i))>
			<cfset getPlugin("messagebox").setMessage("info","User(s) removed successfully")>
		</cfloop>
		<cfset setNextRoute("users/dspUsers")>
	</cffunction>

	<cffunction name="dspAddUser" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<cfset var rc = event.getCollection()>
		<!--- Set the View To Display, after Logic --->
		<cfset Event.setView("vwAdd")>

	</cffunction>

	<cffunction name="doAdd" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<cfset var rc = event.getCollection()>
		<cfset var oUser = "">
		<!--- Get Transfer Object --->
		<cfset oUser = instance.transfer.new("users.users")>
		<!--- Populate it with RC data --->
		<cfset getPlugin("beanFactory").populateBean(oUser)>
		<!--- validate the user with VT --->
		<cfset rc.VTResult = getMyPlugin("ValidateThisCBPlugin").validate(theObject=oUser,objectType="users.users") />
		<cfif rc.VTResult.getIsSuccess()>
			<!--- Update User --->
			<cfset instance.transfer.save(oUser)>
			<cfset getPlugin("messagebox").setMessage("info", "User inserted")>
			<!--- RElocate to listing --->
			<cfset setNextRoute("users/dspUsers")>
		<cfelse>
			<cfset rc.oUser = oUser>
			<cfset Event.setView("vwAdd")>
		</cfif>
	</cffunction>

	<cffunction name="dspEditUser" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<cfset var rc = event.getCollection()>
		<!--- Get User id --->
		<cfset rc.oUser = instance.transfer.get("users.users",rc.id)>
		<!--- Set the View To Display, after Logic --->
		<cfset Event.setView("vwEdit")>
		
	</cffunction>

	<cffunction name="doUpdate" access="public" returntype="void" output="false">
		<cfargument name="Event" type="coldbox.system.beans.requestContext">
		<cfset var rc = event.getCollection()>
		<cfset var oUser = "">
		<!--- Get Transfer Object --->
		<cfset oUser = instance.transfer.get("users.users",rc.id)>
		<!--- Populate it with RC data --->
		<cfset getPlugin("beanFactory").populateBean(oUser)>
		<!--- validate the user with VT --->
		<cfset rc.VTResult = getMyPlugin("ValidateThisCBPlugin").validate(theObject=oUser,objectType="users.users") />
		<cfif rc.VTResult.getIsSuccess()>
			<!--- Update User --->
			<cfset instance.transfer.save(oUser)>
			<cfset getPlugin("messagebox").setMessage("info", "User Updated")>
			<!--- RElocate to listing --->
			<cfset setNextRoute("users/dspUsers")>
		<cfelse>
			<cfset rc.oUser = oUser>
			<cfset Event.setView("vwEdit")>
		</cfif>
	</cffunction>

</cfcomponent>